#*###################
	Bouncing Ball
###################*#

#*######################################
	Variables
######################################*#
var w = canvas.width();		#width of the field
var h = canvas.height();	#height of the field
var intro_ballfall = ((h/2-30)//2)*2;	#special height for the intro animation
var px1 = 300;		#x1 of the bar
var px2	= 380; 		#x2 of the bar
var ball_pos1;	#ball position x
var ball_pos2;	#ball position y
var ball_speed1 = 1;	#ball speed x
var ball_speed2 = 1;	#ball speed y
var score = 0;		#score counter
var colorchange = 0;	#indicator variable for colorchanges
var dead = false;	#if dead then = true
var r, g, b;	#for color change of background and Ball
var collision;
var randomstartx;
#*#########################################
	Functions
#########################################*#
function randomcolor()
{
	if colorchange == true then
	{
		r = math.random();
		g = math.random();
		b = math.random();
	}		
}

function background()	#background
{
	if score >= 5 then
	{	
		if colorchange == true then 
		{	
			randomcolor();
			canvas.setFillColor(r, g, b);			
			canvas.frameRect(0, 0, w, h);
			colorchange = 0;
		}
		else
		{		
			canvas.setFillColor(r, g, b);			
			canvas.frameRect(0, 0, w, h);
		}
	}
	else
	{
		canvas.setFillColor(0, 0.5, 0.7);	#blue background			
		canvas.frameRect(0, 0, w, h);		#background with backgroundcolor
	}
}

function bar(px1, px2)
{
	canvas.line(px1, 500, px2, 500);
}

function randomstart()
{
	var pos1 = math.random()*w;
	var pos2 = math.random()*h;
	if pos1 <= w-150 and pos1 >= 50 then ball_pos1 = pos1; else ball_pos1=w-150-50*math.random();
	if pos2 <= 350 and pos2 >= 50  then ball_pos2 = pos2; else ball_pos2=350-50-50*math.random();
}

function difficulty()
{
	if collision == true then
	{
		if score == 2 then
		{
			ball_speed1 += 1;
			ball_speed2 += -1;
			collision = false;
		}
		if score % 5 == 0 then
		{
			ball_speed1 += math.random();
			ball_speed2 += -math.random();
			collision = false;
		}
	}
}

function death()
{
	background();
	canvas.setFillColor(1, 1, 1);
	canvas.setFont("Arial", w/20);	
	canvas.text(w/2, h/2-h/6, "GAME OVER");
	canvas.text(w/2, h/2-h/10, "press \"r\" to restart");
	canvas.text(w/2, h/2, "Score: " + score);
}
###########################################
			#Intro
function intro()	#Startscreen
{
	canvas.setFont("Arial", w/20);	
	canvas.setTextAlign("center");
	background();
	
	for var i in 0:30 do
	{
		canvas.setFillColor(1, 1, 1, i*0.004);
		canvas.text(w/2, h/2, "Welcome");
		wait(60);
	}
	background();
	for var i in 0:30 do
	{
		canvas.setFillColor(1, 1, 1, i*0.004);
		canvas.text(w/2, h/2, "to");
		wait(60);	
	}
	for var i in 0:30 do
	{
		background();
		canvas.setFillColor(1, 1, 1, i*0.03);
		canvas.setFont("Arial", w/20+(i));
		canvas.text(w/2, h/2-i, "Bouncing Ball");
		canvas.setFont("Arial", w/25);
		canvas.text(w/2+w/4, h/2+h/4, "left = \"a\" right=\"d\"");
		wait(60);
	}
	for var i in 0:intro_ballfall do
	{
		canvas.setFillColor(1, 1, 1, 30*0.03);
		canvas.setFont("Arial", w/20+30);
		canvas.fillCircle(-32+i, i-30, 30);
		canvas.text(w/2, h/2-30, "Bouncing Ball");
		canvas.setFont("Arial", w/25);
		canvas.text(w/2+w/4, h/2+h/4, "left = \"a\" right=\"d\"");
		wait(0.1);
		background();
	}
	for var i in 0:intro_ballfall*2 do
	{
		background();
		canvas.setFillColor(1, 1, 1, 30*0.03);
		canvas.setFont("Arial", w/20+30);
		canvas.fillCircle(intro_ballfall-32+i, intro_ballfall-30-i, 30);
		canvas.text(w/2, h/2-30+i, "Bouncing Ball");
		canvas.setFont("Arial", w/25);
		canvas.text(w/2+w/4, h/2+h/4+i, "left = \"a\" right=\"d\"");
		wait(0.1);	
	}
}

#*#########################################
	game engine
#########################################*#
function checkforcollison(px1, px2)
{
	if ball_pos1 >= w-21 then ball_speed1 = ball_speed1*(-1);
	if ball_pos1 <= +21 then ball_speed1 = ball_speed1*(-1);
	if ball_pos2 <= +21 then ball_speed2 = ball_speed2*(-1);
	
	if ball_pos2 >= 474 and ball_pos2 <= 480 then
	{
		if ball_pos1 >= px1-20 and ball_pos1 <= px2+20 then 
		{
			ball_speed2 = ball_speed2*(-1);
			collision = true;
			score += 1;
			colorchange = true;
			{
				use namespace math;
				use namespace audio;

				var durationInSeconds = 0.1;
				var l_freq = 200; # frequency in Hz
				var r_freq = 400;
				var sampleRate = 8000; # sampleRate in Hz

				var l_samples = [];
				var r_samples = [];

				for var i in 0:(sampleRate * durationInSeconds) do
				{
					l_samples.push(sin( i * (l_freq / sampleRate) * 2 * pi()));
				}

				for var i in 0:(sampleRate * durationInSeconds) do
				{
					r_samples.push(sin( i * (r_freq / sampleRate) * 2 * pi()));
				}

				var sound = StereoSound(l_samples, r_samples, sampleRate);
				sound.play();
			}
		}
	}
	else
	{
		if ball_pos2 > 500 then 
		{
			dead = true;
		}
	}
}

function ball()
{	
	checkforcollison(px1, px2);	#check if ball touches a border
	background();	#call for background
	canvas.circle(ball_pos1, ball_pos2, 20);	#draws the ball
	canvas.setFillColor(0,0,0);	
	canvas.setFont("Arial", 15);	
	canvas.text(ball_pos1+1, ball_pos2-5, "RUB");	#draws "RUB" in the ball
	bar(px1, px2);	#call for bar
	ball_pos1 += ball_speed1;	#move ball x
	ball_pos2 += ball_speed2;	#move ball y

}

###########################################
			#pre start configurations
background();
canvas.setTextAlign("center");
canvas.setFont("Arial", w/20);
canvas.setFillColor(1, 1, 1);
canvas.text(w/2, h/2, "Press Space to start");
canvas.text(w/2, h/2+(w/20)*2, "Press Esc to skip the Intro");
randomstart();

###########################################
			#Game
function onKeyDownpreStart(event)
{
	if event.key == "Escape" then
	{
		quitEventMode(result = null);
	}
	else if event.key == " " then 
	{
		intro();
		quitEventMode(result = null);
	}
}

function onKeyDownGame(event)
{	
	if event.key == "d" then
	{
		if px2 >= w then {px1 = w-80; px2 = w;}
		else {px2 += 10; px1 += 10;}
		bar(px1, px2);
	}
	if event.key == "a" then
	{
		if px1 <= 0 then {px1 = 0; px2 = 80;}
		else {px1 -= 10; px2 -= 10;}
		bar(px1, px2);
	}
	if event.key == "r" then
	{
		dead = false;
		score = 0;
		ball_speed1 = 1;
		ball_speed2 = 1;
		randomstart();
	}
	if event.key == "Escape" then
	{
		var exit = confirm("exit game");
		if exit then quitEventMode();
	}
}

function onTick(event) #things that happens every tick
{
	setEventHandler("canvas.keydown", onKeyDownGame);
	if dead == false then
	{
		ball();
		difficulty();
		canvas.setFillColor(1,1,1);
		canvas.setFont("Arial", w/25);
		canvas.text(0+w/10, h-h/10, "Score: " + score);
	}
	else
	{
		death();	
	}
}

setEventHandler("canvas.keydown", onKeyDownpreStart);
enterEventMode();
setEventHandler("timer", onTick); 
enterEventMode();
