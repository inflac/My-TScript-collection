#*###################
	Bouncing Ball
###################*#

#*######################################
	Variables
######################################*#
var w = canvas.width();		#width of the field
var h = canvas.height();	#height of the field
var intro_ballfall = ((h/2-30)//2)*2; #special height for the intro animation
var px1 = 300; #x1 of the bar
var px2	= 380; #x2 of the bar
var ball_pos1 = 200;
var ball_pos2 = 200;
var ball_speed1 = 1;
var ball_speed2 = 1;
var score = 0;
var difficult = 0;
var dead = false;

#*#########################################
	Functions
#########################################*#
function background()	#background
{
	canvas.setFillColor(0, 0.5, 0.7);	#blue background			
	canvas.frameRect(0, 0, w, h);		#background with backgroundcolor
}

function bar(px1, px2)
{
	canvas.line(px1, 500, px2, 500);
}

function death()
{
	background();
	canvas.setFillColor(1, 1, 1);
	canvas.setFont("Arial", w/20);	
	canvas.text(w/2, h/2-h/6, "GAME OVER");
	canvas.text(w/2, h/2-h/10, "press \"r\" to restart");
	canvas.text(w/2, h/2, "Score: " + score);

}

###########################################
			#Intro
function intro()	#Startscreen
{
	function background()
	{
	canvas.setFillColor(0, 0.5, 0.7);				
	canvas.frameRect(0, 0, w, h);	
	}
	
	canvas.setFont("Arial", w/20);	
	canvas.setTextAlign("center");
	background();
	
	for var i in 0:30 do
	{
		canvas.setFillColor(1, 1, 1, i*0.004);
		canvas.text(w/2, h/2, "Welcome");
		wait(60);
	}
	background();
	for var i in 0:30 do
	{
		canvas.setFillColor(1, 1, 1, i*0.004);
		canvas.text(w/2, h/2, "to");
		wait(60);	
	}
	for var i in 0:30 do
	{
		background();
		canvas.setFillColor(1, 1, 1, i*0.03);
		canvas.setFont("Arial", w/20+(i));
		canvas.text(w/2, h/2-i, "Bouncing Ball");
		wait(60);
	}
	for var i in 0:intro_ballfall do
	{
		canvas.setFillColor(1, 1, 1, 30*0.03);
		canvas.setFont("Arial", w/20+30);
		canvas.fillCircle(-32+i, i-30, 30);
		canvas.text(w/2, h/2-30, "Bouncing Ball");
		wait(0.1);
		background();
	}
	for var i in 0:intro_ballfall*2 do
	{
		background();
		canvas.setFillColor(1, 1, 1, 30*0.03);
		canvas.setFont("Arial", w/20+30);
		canvas.fillCircle(intro_ballfall-32+i, intro_ballfall-30-i, 30);
		canvas.text(w/2, h/2-30+i, "Bouncing Ball");
		wait(0.1);	
	}
}

#*#########################################
	game engine
#########################################*#
function checkforcollison(px1, px2)
{
	if ball_pos1 >= w-21 then ball_speed1 = ball_speed1*(-1);
	if ball_pos1 <= +21 then ball_speed1 = ball_speed1*(-1);
	if ball_pos2 >= h-21 then ball_speed2 = ball_speed2*(-1);
	if ball_pos2 <= +21 then ball_speed2 = ball_speed2*(-1);
	
	if ball_pos2 == 474 then
	{
		if ball_pos1 >= px1-20 and ball_pos1 <= px2+20 then 
		{
			ball_speed2 = ball_speed2*(-1);
			score += 1;
		}
	}
	else
	{
		if ball_pos2 > 500 then 
		{
			dead = true;
		}
	}
}

function ball()		#draws the ball
{	
	checkforcollison(px1, px2);
	canvas.setFillColor(0, 0.6, 0.4);
	canvas.frameRect(0, 0, w, h);
	canvas.circle(ball_pos1+ball_speed1, ball_pos2+ball_speed2, 20);
	canvas.setFillColor(0,0,0);
	canvas.setFont("Arial", 15);
	canvas.text(ball_pos1+1, ball_pos2-5, "RUB");
	bar(px1, px2);
	ball_pos1 += ball_speed1;
	ball_pos2 += ball_speed2;
}

###########################################
			#pre start configurations
background();
canvas.setTextAlign("center");
canvas.setFont("Arial", w/20);
canvas.setFillColor(1, 1, 1);
canvas.text(w/2, h/2, "Press Space to start");
canvas.text(w/2, h/2+(w/20)*2, "Press Esc to skip the Intro");

###########################################
			#Game
function onKeyDownpreStart(event)
{
	if event.key == "Escape" then
	{
		quitEventMode(result = null);
	}
	else if event.key == " " then 
	{
		intro();
		quitEventMode(result = null);
	}
}

function onKeyDownGame(event)
{	
	if event.key == "d" then
	{
		if px2 >= w then {px1 = w-80; px2 = w;}
		else {px2 += 10; px1 += 10;}
		bar(px1, px2);
	}
	if event.key == "a" then
	{
		if px1 <= 0 then {px1 = 0; px2 = 80;}
		else {px1 -= 10; px2 -= 10;}
		bar(px1, px2);
	}
	if event.key == "r" then
	{
		dead = false;
		score = 0;
		ball_pos1 = 200;
		ball_pos2 = 200;
	}
	if event.key == "Escape" then
	{
		var exit = confirm("exit game");
		if exit then quitEventMode();
	}
}

function onTick(event) #things that happens every tick
{
	setEventHandler("canvas.keydown", onKeyDownGame);
	if dead == false then
	{
		ball();
		canvas.setFillColor(1,1,1);
		canvas.setFont("Arial", w/25);
		canvas.text(0+w/10, h-h/10, "Score: " + score);
	}
	else
	{
		death();	
	}
}

setEventHandler("canvas.keydown", onKeyDownpreStart);
enterEventMode();
setEventHandler("timer", onTick); 
enterEventMode();

